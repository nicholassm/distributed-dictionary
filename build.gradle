/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'nicholassm' at '29-02-16 18:33' with Gradle 2.9
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/2.9/userguide/tutorial_java_projects.html
 */

// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group            = "systems.speedy.distributed"
archivesBaseName = "dictionary"
version          = "1.0.0"

repositories {
	mavenCentral()
}

// In this section you declare the dependencies for your production and test code
dependencies {
	testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom.project {
				name 'Distributed Dictionary'
				packaging 'jar'

				description 'A library that simplifies sharing a dictionary across processes to minimize network traffic for often repeated strings.'
				url 'https://github.com/nicholassm/distributed-dictionary'

				scm {
					connection 'scm:git:git://github.com/nicholassm/distributed-dictionary'
					developerConnection 'scm:git:git://github.com/nicholassm/distributed-dictionary'
					url 'https://github.com/nicholassm/distributed-dictionary'
				}

				licenses {
					license {
						name 'The MIT License'
						url 'https://opensource.org/licenses/MIT'
					}
				}

				developers {
					developer {
						id 'nicholassm'
						name 'Nicholas Schultz-MÃ¸ller'
						email 'nicholassm@gmail.com'
					}
				}
			}
		}
	}
}
